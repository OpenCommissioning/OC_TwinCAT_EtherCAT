<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_DS402_FSM" Id="{a0dc8ba2-6d90-0721-10b8-a5af0408b39b}" SpecialFunc="None">
    <Declaration><![CDATA[(*
	DS402 Finite State Machine General
*)
FUNCTION_BLOCK FB_DS402_FSM
VAR_INPUT	
	stControl					: ST_DS402_ControlWord;
END_VAR

VAR_OUTPUT	
	stStatus					: ST_DS402_StatusWord;	
END_VAR

VAR	
	_eState 					: E_DS402_State := E_DS402_State.S0_NotReadyToSwitchOn;	
	_rTrig_QuickStop 			: R_TRIG;
	_rTrig_Reset 				: R_TRIG;
	_bError						: BOOL;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[_rTrig_QuickStop(CLK := stControl.b2_QuickStop);
_rTrig_Reset(CLK := stControl.b7_FaultReset);

CASE _eState OF
	E_DS402_State.S0_NotReadyToSwitchOn : S0_NotReadyToSwitchOn();
	E_DS402_State.S1_SwitchOnDisabled : S1_SwitchOnDisabled();
	E_DS402_State.S2_ReadyToSwitchOn : S2_ReadyToSwitchOn();
	E_DS402_State.S3_SwitchedOn : S3_SwitchedOn();
	E_DS402_State.S4_OperationEnabled : S4_OperationEnabled();
	E_DS402_State.S5_Fault : S5_Fault();	
	E_DS402_State.S6_FaultReactionActive : S6_FaultReactionActive();	
	E_DS402_State.S7_QuickStopActive : S7_QuickStopActive();			
END_CASE]]></ST>
    </Implementation>
    <Property Name="bError" Id="{97e02667-2aae-0fd0-16cc-362c305190cb}">
      <Declaration><![CDATA[PROPERTY PUBLIC bError : BOOL]]></Declaration>
      <Get Name="Get" Id="{193638ce-a95d-00d3-080a-13539a5d868f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bError := _bError;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{ab26488d-38fe-020a-3df4-65e37075bd2a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF bError AND NOT _bError THEN
	_bError := TRUE;
END_IF

]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="eState" Id="{741fc733-75e8-029a-01c4-618973fa6d1a}">
      <Declaration><![CDATA[PROPERTY eState : E_DS402_State]]></Declaration>
      <Get Name="Get" Id="{93756238-2826-02ae-0b02-5dadab8ed5d0}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[eState := _eState;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{c32955bf-68c1-0b24-23ff-d39f6493c8b7}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_eState := eState;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="IsDisableOperation" Id="{4af15465-03eb-0ef9-0ddc-1c21d306311c}">
      <Declaration><![CDATA[METHOD PRIVATE IsDisableOperation : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsDisableOperation := 
stControl.b0_SwitchOn 
AND stControl.b1_EnableVoltage 
AND stControl.b2_QuickStop 
AND NOT stControl.b3_EnableOperation
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsDisableVoltage" Id="{b861cf89-e9ab-08b5-03a7-d859372f04ca}">
      <Declaration><![CDATA[METHOD PRIVATE IsDisableVoltage : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsDisableVoltage := 
NOT stControl.b1_EnableVoltage
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsEnableOperation" Id="{7e5e0b06-06d4-0bcb-1ce3-153bce4f601d}">
      <Declaration><![CDATA[METHOD PRIVATE IsEnableOperation : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsEnableOperation := 
stControl.b0_SwitchOn 
AND stControl.b1_EnableVoltage 
AND stControl.b2_QuickStop 
AND stControl.b3_EnableOperation
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsFaultReset" Id="{eba60555-d2e0-01c9-2d86-d9c1aba97b93}">
      <Declaration><![CDATA[METHOD PRIVATE IsFaultReset : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsFaultReset := _rTrig_Reset.Q;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsQuickStop" Id="{cace8c55-da78-0808-02c3-4c36634dc258}">
      <Declaration><![CDATA[METHOD PRIVATE IsQuickStop : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsQuickStop := 
NOT stControl.b2_QuickStop
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsShutdown" Id="{bfea2390-a496-06b7-2fd5-12c8a0377bb9}">
      <Declaration><![CDATA[METHOD PRIVATE IsShutdown : BOOL


]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsShutdown := 
NOT stControl.b0_SwitchOn 
AND stControl.b1_EnableVoltage 
AND stControl.b2_QuickStop
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsSwitchOn" Id="{4ae2ff82-e6d7-06e9-34b7-3387ddd9781c}">
      <Declaration><![CDATA[METHOD PRIVATE IsSwitchOn : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[IsSwitchOn := 
stControl.b0_SwitchOn 
AND stControl.b1_EnableVoltage 
AND stControl.b2_QuickStop
AND NOT stControl.b7_FaultReset;]]></ST>
      </Implementation>
    </Method>
    <Property Name="nControl" Id="{52160851-a300-02c2-2ef6-fc5f90b776cc}">
      <Declaration><![CDATA[PROPERTY nControl : WORD]]></Declaration>
      <Get Name="Get" Id="{20fdd28a-b24c-0c41-180c-970eee354c2f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nControl.0	:= stControl.b0_SwitchOn;					         
nControl.1	:= stControl.b1_EnableVoltage;				      	
nControl.2	:= stControl.b2_QuickStop;					         	
nControl.3	:= stControl.b3_EnableOperation;			     	
nControl.4	:= stControl.b4_OperationModeSpecific;			
nControl.5	:= stControl.b5_OperationModeSpecific;			
nControl.6	:= stControl.b6_OperationModeSpecific;			
nControl.7	:= stControl.b7_FaultReset;					        	
nControl.8	:= stControl.b8_Reserved;					          	
nControl.9	:= stControl.b9_Reserved;					          
nControl.10	:= stControl.b10_Reserved;					         	
nControl.11	:= stControl.b11_Reserved;					         	
nControl.12	:= stControl.b12_Reserved;					         	
nControl.13	:= stControl.b13_Reserved;					         	
nControl.14	:= stControl.b14_Reserved;					         	
nControl.15 := stControl.b15_Reserved;					         ]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{bc46abfc-958c-0a00-0c7f-4d628c9cb3df}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[stControl.b0_SwitchOn 					:= 	nControl.0;	
stControl.b1_EnableVoltage				:= 	nControl.1;		
stControl.b2_QuickStop					:= 	nControl.2;		
stControl.b3_EnableOperation			:= 	nControl.3;		
stControl.b4_OperationModeSpecific		:= 	nControl.4;		
stControl.b5_OperationModeSpecific		:= 	nControl.5;		
stControl.b6_OperationModeSpecific		:= 	nControl.6;		
stControl.b7_FaultReset					:= 	nControl.7;		
stControl.b8_Reserved					:= 	nControl.8;		
stControl.b9_Reserved					:= 	nControl.9;		
stControl.b10_Reserved					:= 	nControl.10;		
stControl.b11_Reserved					:= 	nControl.11;		
stControl.b12_Reserved					:= 	nControl.12;		
stControl.b13_Reserved					:= 	nControl.13;		
stControl.b14_Reserved					:= 	nControl.14;		
stControl.b15_Reserved					:= 	nControl.15;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="nStatus" Id="{e46eecf1-e520-0256-1012-81fc316d0e85}">
      <Declaration><![CDATA[PROPERTY nStatus : WORD]]></Declaration>
      <Get Name="Get" Id="{ea0c72e9-5f9e-0469-005f-4e45e468be85}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[nStatus.0 := stStatus.b0_ReadyToSwitchOn; 			
nStatus.1 := stStatus.b1_SwitchedOn; 				
nStatus.2 := stStatus.b2_OperationEnabled;			
nStatus.3 := stStatus.b3_Fault;					
nStatus.4 := stStatus.b4_VoltageEnabled;
nStatus.5 := stStatus.b5_QuickStop;
nStatus.6 := stStatus.b6_SwitchOnDisabled;			
nStatus.7 := stStatus.b7_Warning;					
nStatus.8 := stStatus.b8_Synchranisation;			
nStatus.9 := stStatus.b9_Remote;			
nStatus.10 := stStatus.b10_TargetReached;				
nStatus.11 := stStatus.b11_InternalLimitActive;		
nStatus.12 := stStatus.b12_OperationModeSpecific;			
nStatus.13 := stStatus.b13_OperationModeSpecific;			
nStatus.14 := stStatus.b14_OperationModeSpecific;			
nStatus.15 := stStatus.b15_ClosedLoopActive;	]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ResetError" Id="{c4a514f9-d5e3-0237-082c-bf1a927ba4ce}">
      <Declaration><![CDATA[METHOD INTERNAL ResetError : BOOL

]]></Declaration>
      <Implementation>
        <ST><![CDATA[_bError := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="S0_NotReadyToSwitchOn" Id="{26489511-515a-0c9c-1b96-0d3312bb194d}">
      <Declaration><![CDATA[METHOD PRIVATE S0_NotReadyToSwitchOn : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= FALSE;
stStatus.b1_SwitchedOn 				:= FALSE;
stStatus.b2_OperationEnabled		:= FALSE;
stStatus.b3_Fault					:= FALSE;
stStatus.b4_VoltageEnabled			:= FALSE;
stStatus.b6_SwitchOnDisabled 		:= FALSE;

_eState := E_DS402_State.S1_SwitchOnDisabled;]]></ST>
      </Implementation>
    </Method>
    <Method Name="S1_SwitchOnDisabled" Id="{e90d155a-60b3-03fb-1e3b-f2b289f9a1b7}">
      <Declaration><![CDATA[METHOD PRIVATE S1_SwitchOnDisabled : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= FALSE;
stStatus.b1_SwitchedOn				:= FALSE;
stStatus.b2_OperationEnabled 		:= FALSE;
stStatus.b3_Fault 					:= FALSE;
stStatus.b4_VoltageEnabled			:= FALSE;
stStatus.b6_SwitchOnDisabled 		:= TRUE;

IF IsShutdown() THEN
	_eState := E_DS402_State.S2_ReadyToSwitchOn;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="S2_ReadyToSwitchOn" Id="{08525161-31c4-0e56-0860-878658c0b2b5}">
      <Declaration><![CDATA[METHOD PRIVATE S2_ReadyToSwitchOn : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= TRUE;
stStatus.b1_SwitchedOn 				:= FALSE;
stStatus.b2_OperationEnabled 		:= FALSE;
stStatus.b3_Fault 					:= FALSE;
stStatus.b4_VoltageEnabled			:= TRUE;
stStatus.b5_QuickStop 				:= TRUE; 
stStatus.b6_SwitchOnDisabled 		:= FALSE;

IF _bError THEN
	_eState := E_DS402_State.S6_FaultReactionActive;
	RETURN;
END_IF

IF IsDisableVoltage() OR IsQuickStop() THEN
	_eState := E_DS402_State.S1_SwitchOnDisabled;
	RETURN;
END_IF

IF IsSwitchOn() THEN
	_eState := E_DS402_State.S3_SwitchedOn;
	RETURN;
END_IF


]]></ST>
      </Implementation>
    </Method>
    <Method Name="S3_SwitchedOn" Id="{ac948c15-1851-052b-01e7-dc360153e1c3}">
      <Declaration><![CDATA[METHOD PRIVATE S3_SwitchedOn : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= TRUE;
stStatus.b1_SwitchedOn 				:= TRUE;
stStatus.b2_OperationEnabled 		:= FALSE;
stStatus.b3_Fault 					:= FALSE;
stStatus.b5_QuickStop 				:= TRUE; 
stStatus.b6_SwitchOnDisabled 		:= FALSE;

IF _bError THEN
	_eState := E_DS402_State.S6_FaultReactionActive;
	RETURN;
END_IF

IF IsDisableVoltage() OR IsQuickStop() THEN
	_eState := E_DS402_State.S1_SwitchOnDisabled;
	RETURN;
END_IF

IF IsShutdown() THEN
	_eState := E_DS402_State.S2_ReadyToSwitchOn;
	RETURN;
END_IF

IF IsEnableOperation() THEN
	_eState := E_DS402_State.S4_OperationEnabled;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="S4_OperationEnabled" Id="{a5bebf6e-e2bd-0873-2e4e-94217ca8a753}">
      <Declaration><![CDATA[METHOD PRIVATE S4_OperationEnabled : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn				:= TRUE;
stStatus.b1_SwitchedOn 					:= TRUE;
stStatus.b2_OperationEnabled 			:= TRUE;
stStatus.b3_Fault 						:= FALSE;
stStatus.b5_QuickStop 					:= TRUE; 
stStatus.b6_SwitchOnDisabled 			:= FALSE;

IF _bError THEN
	_eState := E_DS402_State.S6_FaultReactionActive;
	RETURN;
END_IF

IF IsDisableVoltage() THEN
	_eState := E_DS402_State.S1_SwitchOnDisabled;
	RETURN;
END_IF

IF IsShutdown() THEN
	_eState := E_DS402_State.S2_ReadyToSwitchOn;
	RETURN;
END_IF

IF IsQuickStop() THEN
	_eState := E_DS402_State.S7_QuickStopActive;
	RETURN;
END_IF

IF IsDisableOperation() THEN
	_eState := E_DS402_State.S3_SwitchedOn;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="S5_Fault" Id="{4414ec90-ecb1-0d9a-2dcc-6ef2e112368b}">
      <Declaration><![CDATA[METHOD PRIVATE S5_Fault : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= FALSE;
stStatus.b1_SwitchedOn				:= FALSE;
stStatus.b2_OperationEnabled 		:= FALSE;
stStatus.b3_Fault 					:= TRUE;
stStatus.b6_SwitchOnDisabled 		:= FALSE;

IF IsFaultReset() THEN
	_bError := FALSE;
	_eState := E_DS402_State.S1_SwitchOnDisabled;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="S6_FaultReactionActive" Id="{cc5453c9-f2a5-01ee-0251-3f533481a81a}">
      <Declaration><![CDATA[METHOD PRIVATE S6_FaultReactionActive : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= TRUE;
stStatus.b1_SwitchedOn 				:= TRUE;
stStatus.b2_OperationEnabled 		:= TRUE;
stStatus.b3_Fault 					:= TRUE;
stStatus.b6_SwitchOnDisabled 		:= FALSE;

_eState := E_DS402_State.S5_Fault;]]></ST>
      </Implementation>
    </Method>
    <Method Name="S7_QuickStopActive" Id="{74233ae8-5bfd-0a9d-28a7-6108cf151752}">
      <Declaration><![CDATA[METHOD PRIVATE S7_QuickStopActive : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stStatus.b0_ReadyToSwitchOn 		:= TRUE;
stStatus.b1_SwitchedOn 				:= TRUE;
stStatus.b2_OperationEnabled 		:= TRUE;
stStatus.b3_Fault 					:= FALSE;
stStatus.b5_QuickStop 				:= FALSE; 
stStatus.b6_SwitchOnDisabled		:= FALSE;

IF IsDisableVoltage() THEN
	_eState := E_DS402_State.S1_SwitchOnDisabled;
	RETURN;
END_IF

IF IsEnableOperation() THEN
	_eState := E_DS402_State.S4_OperationEnabled;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>